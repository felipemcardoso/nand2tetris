// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    //Increment the current value of register
    Inc16(in=outRegister, out=outIncremented);
    
    //Current or incremented value of register 
    Mux16(a=outRegister, b=outIncremented, sel=inc, out=outIncMux);    

    //If load=1, output will be in, otherwise, value of previous mux.
    Mux16(a=outIncMux, b=in, sel=load, out=outLoadMux);

    //if reset=1, output will be Zero, otherwise, value of previous mux.
    Mux16(a=outLoadMux, b=false, sel=reset, out=outResetMux);

    //Always refresh the register
    Register(in=outResetMux, load=true, out=outRegister, out=out);
}
